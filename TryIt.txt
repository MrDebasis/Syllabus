@AuraEnabled
public static void upsertMilestones(String payloadJson, Id caseId, String surveyType) {
    try {
        List<Object> rawList = (List<Object>) JSON.deserializeUntyped(payloadJson);
        List<MilestoneWrapper> incomingList = new List<MilestoneWrapper>();

        for (Object rawItem : rawList) {
            Map<String, Object> mapItem = (Map<String, Object>) rawItem;
            MilestoneWrapper wrapper = new MilestoneWrapper();
            wrapper.UniqueId = (String) mapItem.get('UniqueId');
            wrapper.label = (String) mapItem.get('label');
            wrapper.value = (String) mapItem.get('value');
            if (mapItem.containsKey('priority')) {
                wrapper.priority = String.valueOf(mapItem.get('priority'));
            }
            incomingList.add(wrapper);
        }

        Set<String> uniqueIds = new Set<String>();
        for (MilestoneWrapper wrap : incomingList) {
            uniqueIds.add(wrap.UniqueId);
        }

        Id recordTypeId = Schema.SObjectType.PatientSupportMilestone__c
            .getRecordTypeInfosByName()
            .get('PatientPortalSurvey')
            .getRecordTypeId();

        Map<String, PatientSupportMilestone__c> existingMap = new Map<String, PatientSupportMilestone__c>();
        for (PatientSupportMilestone__c ms : [
            SELECT Id, UniqueId__c 
            FROM PatientSupportMilestone__c 
            WHERE Case__c = :caseId 
            AND SurveyType__c = :surveyType 
            AND UniqueId__c IN :uniqueIds
        ]) {
            existingMap.put(ms.UniqueId__c, ms);
        }

        List<PatientSupportMilestone__c> recordsToUpsert = new List<PatientSupportMilestone__c>();

        for (MilestoneWrapper wrapper : incomingList) {
            PatientSupportMilestone__c record;

            if (existingMap.containsKey(wrapper.UniqueId)) {
                record = existingMap.get(wrapper.UniqueId);
            } else {
                record = new PatientSupportMilestone__c();
                record.Case__c = caseId;
                record.SurveyType__c = surveyType;
                record.UniqueId__c = wrapper.UniqueId;
                record.RecordTypeId = recordTypeId;
            }

            record.Question__c = wrapper.label;
            record.SurveyStatus__c = 'Completed';

            if (!String.isBlank(wrapper.priority)) {
                record.Priority__c = wrapper.priority;
                record.QuestionValue__c = null;

                if (wrapper.label != null && wrapper.label.toLowerCase().contains('arrival')) {
                    record.ArrivalDate__c = Date.valueOf(wrapper.value);
                    record.DepartureDate__c = null;
                } else if (wrapper.label != null && wrapper.label.toLowerCase().contains('departure')) {
                    record.DepartureDate__c = Date.valueOf(wrapper.value);
                    record.ArrivalDate__c = null;
                }
            } else {
                record.Priority__c = null;
                record.QuestionValue__c = wrapper.value;
                record.ArrivalDate__c = null;
                record.DepartureDate__c = null;
            }

            recordsToUpsert.add(record);
        }

        upsert recordsToUpsert;

    } catch (Exception e) {
        throw new AuraHandledException('Error processing milestone data: ' + e.getMessage());
    }
}

public class MilestoneWrapper {
    public String UniqueId;
    public String label;
    public String value;
    public String priority;
}
